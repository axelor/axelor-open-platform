description = 'Axelor\'s Front-end'

buildscript {
    repositories repos
    dependencies {
        classpath "com.github.node-gradle:gradle-node-plugin:3.5.1"
    }
}

apply plugin: 'base'
apply plugin: 'com.github.node-gradle.node'

node {
    version = '18.14.0'
    pnpmVersion = '7.28.0'
    download = true
}

task buildFront(type: PnpmTask) {
    group = 'axelor'
    description = 'Build Front-end'

    outputs.dir('dist')
    inputs.files('package.json', 'pnpm-lock.yaml')
    inputs.dir('src')
    inputs.dir('public')

    dependsOn 'installFrontDeps'
    args = ["run", "build"]
}

task installFrontDeps(type: PnpmTask) {
    group = 'axelor'
    description = 'Install Front-end dependencies'

    args = ['install']
}

clean {
    delete "dist", "node_modules"
}
