apply plugin: 'application'

buildscript {
	apply from: "version.gradle"
}

dependencies {
	compile project(":axelor-shell")
}

def localDist = "${rootProject.buildDir}/dist"
def localRepo = "${localDist}/maven"

allprojects {

	apply from: "${rootDir}/gradle/repo.gradle"
	apply from: "${rootDir}/gradle/libs.gradle"
	apply from: "${rootDir}/gradle/common.gradle"
	apply from: "${rootDir}/gradle/eclipse.gradle"
	apply from: "${rootDir}/gradle/license.gradle"
	apply from: "${rootDir}/version.gradle"

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	apply plugin: "maven-publish"

	group "com.axelor"

	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
			}
		}
		repositories {
			maven {
				name "adk"
				url "file://${localRepo}"
			}
		}
	}

	afterEvaluate {
		test {
			forkEvery 1
			maxParallelForks 4

			beforeTest { descriptor ->
				logger.lifecycle("Running: " + descriptor)
			}

			onlyIf {
				return project.hasProperty('run-test')
			}
		}
	}
}

task repo(dependsOn: [':axelor-gradle:dist']) {
	outputs.dir localDist
}

tasks.repo.dependsOn subprojects*.publish

jar.enabled = false
install.enabled = false
publish.enabled = false

applicationName = "axelor"
mainClassName = "com.axelor.shell.Main"

run {
	standardInput = System.in
	standardOutput = System.out
}

applicationDistribution.from (repo) {
}
applicationDistribution.from("${rootDir}/gradle") {
	into "gradle"
	include "*.gradle"
}

def distName = "axelor-development-kit"
def distConfig = {
	baseName distName
	eachFile {
		if (it.name.endsWith('.bat')) {
			it.mode = 0644
		}
	}
}

distZip distConfig
distTar distConfig
distTar.compression Compression.GZIP

task dist(dependsOn: [distZip, distTar])

installDist {
	destinationDir = file("${buildDir}/install/${distName}")
}
// deprecated
installApp {
	destinationDir = file("${buildDir}/install/${distName}")
}

task wrapper(type: Wrapper) {
	gradleVersion = "2.8"
}

task updateVersion(type: com.axelor.gradle.tasks.VersionTask) {
	description "Update version text in source files."
	group "Axelor"
	processFiles = fileTree(projectDir) {
		include '**/resources/**/*.xml'
		include '**/resources/domain-models.xsd'
		include '**/resources/object-views.xsd'
		include '**/resources/data-import.xsd'
		include '**/resources/templates/**/*.tmpl'
		include '**/data/**/*config.xml'
		include '**/webapp/package.json'
	}
}

apply from: "gradle/javadoc.gradle"
